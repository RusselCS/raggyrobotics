actor RoboenzaWep_ScriptBar : ScriptBar {}

actor RoboenzaWep : MegaBuster
{
    Weapon.AmmoUse 0
    Weapon.SlotNumber 1
    Weapon.Ammotype "RoboenzaTimer"
    Obituary "%o was eaten alive by %k."
    Inventory.Pickupmessage "Infected!"
    +WEAPON.AMMO_OPTIONAL
    //inventory.icon "INFEWEP"
    Scale 2.0
    States
    {
    Spawn:
        WEA2 Y 0
        stop
    Ready:
        ZOMA D 0 ACS_NamedExecuteWithResult("core_weaponcolor", CLR_INFECTED)
        ZOMA D 1 A_WeaponReady(WRF_DISABLESWITCH)
        Goto Ready+1
    Deselect:
        TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
        ZOMA D 1 A_Lower
        loop
    Select:
        TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
        ZOMA D 1 A_Raise
        Loop
    Fire: // OLD ANIATION: 27 TICKS
        ZOMA D 0 A_GiveInventory("ZombieAttack_P", 1)
        ZOMA EGHIJK 2
        TNT1 A 9
        TNT1 A 0 A_Refire("LeftSwing")
        goto FireEnd
    LeftSwing:
        ZOMA L 0 A_GiveInventory("ZombieAttackR_P", 1)
        ZOMA LMNOPQ 2
        TNT1 A 9
        TNT1 A 0 A_Refire("RightSwing")
        goto FireEnd
    RightSwing:
        ZOMA L 0 A_GiveInventory("ZombieAttack_P", 1)
        ZOMA FGHIJK 2
        TNT1 A 9
        TNT1 A 0 A_Refire("LeftSwing")
        goto FireEnd
    FireEnd: // 6 TICKS
        ZOMA ABC 2
        Goto Ready+1
    AltFire:
        ZOMA C 1 A_JumpIf(momz!=0, "Nope")
        ZOMA C 0 A_JumpIfInventory("RoboenzaTimer", 6, "SupaJump")
        ZOMA C 0 A_JumpIfHealthLower(15, "Nope")
        Goto SupaJump
    Nope:
        Goto Ready+1
    SupaJump:
        ZOMA C 0 A_PlaySoundEx("rroboenza/player/superjump","Voice")
        ZOMA C 0 ThrustThingZ(0, 80, 0, 0)
        ZOMA C 0 A_TakeInventory("JumpCancler",1)
        ZOMA C 0 A_TakeInventory("RoboenzaTimer",5)
        ZOMA C 0 ACS_NamedExecuteWithResult("RROBOENZ_HEALTHSPEND")
        ZOMA CBA 1
        TNT1 A 10
        ZOMA ABC 3
        Goto Ready+1
    Flash:
        TNT1 AAAAAAAAAA 2 A_FireCustomMissile("ClimbChecker",0,0,0,0)
        stop
    }
}

actor ClimbChecker : FastProjectile
{
    PROJECTILE
    +RIPPER
    damage (0)
    Height 16
    Radius 16
    Speed 64
    States
    {
    Spawn:
        TNT1 A 0
        stop
    Death:
        TNT1 A 0
        TNT1 A 1 A_GiveToTarget("Climb",1)
        stop
    }
}

actor Climb : CustomInventory
{
    states
    {
    Pickup:
        TNT1 A 0
        TNT1 A 0 ThrustThingZ(0,20,0,0)
        stop
    }
}

actor ZombieAttack_P : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_JumpIfInventory("RoboenzaAlphaInfected", 1, "LungeCheck")
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_SpawnItemEx("InfectMorphWatcher")
        TNT1 A 0 A_SpawnItemEx("InfectClawWarper", cos(-pitch)*56,0,32+(sin(-pitch)*48))
        TNT1 A 0 A_SpawnItemEx("InfectClawWarperSmallLeft",  cos(-pitch)*56,-15, 38+(sin(-pitch)*48),  15)
        TNT1 A 0 A_SpawnItemEx("InfectClawWarperSmallRight", cos(-pitch)*56, 15, 26+(sin(-pitch)*48), -15)
        stop
    LungeCheck:
        TNT1 A 0 A_JumpIf(CallACS("core_checkfooting"), "Lunge")
        goto Pickup+1
    Lunge:
        TNT1 A 0 A_Recoil(-10)
        goto Pickup+1
    }
}

actor ZombieAttackR_P : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_JumpIfInventory("RoboenzaAlphaInfected", 1, "LungeCheck")
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_SpawnItemEx("InfectMorphWatcher")
        TNT1 A 0 A_SpawnItemEx("InfectClawWarperR", cos(-pitch)*56,0,32+(sin(-pitch)*48))
        TNT1 A 0 A_SpawnItemEx("InfectClawWarperSmallLeftR",  cos(-pitch)*56,-15, 26+(sin(-pitch)*48),  15)
        TNT1 A 0 A_SpawnItemEx("InfectClawWarperSmallRightR", cos(-pitch)*56, 15, 38+(sin(-pitch)*48), -15)
        stop
    LungeCheck:
        TNT1 A 0 A_JumpIf(CallACS("core_checkfooting"), "Lunge")
        goto Pickup+1
    Lunge:
        TNT1 A 0 A_Recoil(-10)
        goto Pickup+1
    }
}

actor InfectMorphWatcher : BasicWatcher
{
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_PlaySoundEx("rroboenza/player/slash","Weapon")
        TNT1 AAA 3 A_GiveToTarget("FireInfectMorpher_P", 1)
        TNT1 A 5
        stop
    }
}

actor InfectClawWarper : BasicProjectile
{
    +NOINTERACTION
    args 0, 32, 56
    Damage (0)
    States
    {
    Spawn:
        TNT1 A 0
        RZCL AABBCCDDEEFF 1 A_GiveInventory("InfectClawWarper_P", 1)
        stop
    }
}

actor InfectClawWarperSmallLeft : InfectClawWarper
{
    scale 1.25
    args -10, 38, 56
}

actor InfectClawWarperSmallRight : InfectClawWarperSmallLeft
{
    args 10, 26, 56
}

actor InfectClawWarperR : BasicProjectile
{
    +NOINTERACTION
    args 0, 32, 56
    Damage (0)
    States
    {
    Spawn:
        TNT1 A 0
        RZC2 AABBCCDDEEFF 1 A_GiveInventory("InfectClawWarper_P", 1)
        stop
    }
}

actor InfectClawWarperSmallLeftR : InfectClawWarperR
{
    scale 1.25
    args -10, 26, 56
}

actor InfectClawWarperSmallRightR : InfectClawWarperSmallLeftR
{
    args 10, 36, 56
}

actor InfectClawWarper_P : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0
        TNT1 A 0 A_SetPitch(CallACS("core_GetPtrPitch", AAPTR_TARGET))
        TNT1 A 0 A_Warp(AAPTR_TARGET, cos(-pitch)*args[2],args[0],args[1]+(sin(-pitch)*48),-args[0],WARPF_INTERPOLATE)
        stop
    }
}

actor FireInfectMorpher_P : CustomInventory { States { Pickup: TNT1 A 0 A_SpawnItemEx ("InfectMorpher",cos(-pitch)*56,0,32+(sin(-pitch)*48), momx, momy, 0,0,SXF_ABSOLUTEMOMENTUM,0) stop }}

actor InfectMorpher : BasicProjectile
{
    Obituary "$OB_ROBOENZAWEP"
    Renderstyle "None"
    Speed 20
    radius 10
    height 6
    damage (20)
    +RIPPER
    damagetype "RoboenzaInfection"
    States
    {
    Spawn:
        RZCL A 3
        stop
    }
}

const int RBNZ_INFECT_RADIUS = 16;

actor InfectionEffect : BasicWatcher
{
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_PlaySoundEx("rroboenza/player/infect","Voice")
        TNT1 A 0 A_SpawnItemEx("InfectionCloud", RBNZ_INFECT_RADIUS, 0, 40, frandom(0.25, 1), 0, frandom(-0.25, -1), 0)
        TNT1 A 0 A_SpawnItemEx("InfectionCloud", RBNZ_INFECT_RADIUS, 0, 40, frandom(0.25, 1), 0, frandom(-0.25, -1), 180)
        TNT1 AA 0 A_SpawnItemEx("InfectionCloud", frandom(0, RBNZ_INFECT_RADIUS), 0, 28+frandom(-RBNZ_INFECT_RADIUS, RBNZ_INFECT_RADIUS), frandom(0.25, 1), 0, frandom(-0.25, -1), frandom(0, 360))
        TNT1 A 1

        TNT1 A 0 A_SpawnItemEx("InfectionCloud", RBNZ_INFECT_RADIUS, 0, 16, frandom(0.25, 1), 0, frandom(-0.25, -1), 45+90)
        TNT1 A 0 A_SpawnItemEx("InfectionCloud", RBNZ_INFECT_RADIUS, 0, 16, frandom(0.25, 1), 0, frandom(-0.25, -1), 45+270)
        TNT1 AA 0 A_SpawnItemEx("InfectionCloud", frandom(0, RBNZ_INFECT_RADIUS), 0, 28+frandom(-RBNZ_INFECT_RADIUS, RBNZ_INFECT_RADIUS), frandom(0.25, 1), 0, frandom(-0.25, -1), frandom(0, 360))
        TNT1 A 1

        TNT1 A 0 A_SpawnItemEx("InfectionCloud", RBNZ_INFECT_RADIUS, 0, 40, frandom(0.25, 1), 0, frandom(-0.25, -1), 90)
        TNT1 A 0 A_SpawnItemEx("InfectionCloud", RBNZ_INFECT_RADIUS, 0, 40, frandom(0.25, 1), 0, frandom(-0.25, -1), 270)
        TNT1 AA 0 A_SpawnItemEx("InfectionCloud", frandom(0, RBNZ_INFECT_RADIUS), 0, 28+frandom(-RBNZ_INFECT_RADIUS, RBNZ_INFECT_RADIUS), frandom(0.25, 1), 0, frandom(-0.25, -1), frandom(0, 360))
        TNT1 A 1

        TNT1 A 0 A_SpawnItemEx("InfectionCloud", RBNZ_INFECT_RADIUS, 0, 16, frandom(0.25, 1), 0, frandom(-0.25, -1), 45+0)
        TNT1 A 0 A_SpawnItemEx("InfectionCloud", RBNZ_INFECT_RADIUS, 0, 16, frandom(0.25, 1), 0, frandom(-0.25, -1), 45+180)
        TNT1 AA 0 A_SpawnItemEx("InfectionCloud", frandom(0, RBNZ_INFECT_RADIUS), 0, 28+frandom(-RBNZ_INFECT_RADIUS, RBNZ_INFECT_RADIUS), frandom(0.25, 1), 0, frandom(-0.25, -1), frandom(0, 360))
        TNT1 A 1

        TNT1 A 2
        stop
    }
}

