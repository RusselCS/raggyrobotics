const int CLR_INFECTED = 55388;

actor BusterShotPain : MegaShot replaces MegaShot { +FORCEPAIN }

actor RoboenzaInfected : Once {}
actor RoboenzaAlphaInfected : Once {}

actor RoboenzaTimerInvBar : CustomInventory
{
    Inventory.amount 5000
    Inventory.maxamount 5000
    +INVBAR
    States
    {
    Use:
        TNT1 A 0
        fail
    }
}

actor RoboenzaTimer : Ammo
{
    Inventory.amount 5000
    Inventory.maxamount 5000
}

actor InfectRecoilTime : PlayerPropertyInvulnerable {}

actor InfectRecoil : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_JumpIfInventory("RoboenzaAlphaInfected", 1, "Forward")
        TNT1 A 0 A_JumpIfInventory("InfectRecoilTime", 1, "None")
        TNT1 A 0 A_GiveInventory("InfectRecoilTime", 2)
        TNT1 A 0 A_Recoil(6)
        stop
    Forward: // Cancels the "standard" hitstun
        TNT1 A 0 A_Recoil(-1)
        TNT1 A 0 A_ScaleVelocity(1.33)
        stop
    }
}

actor RoboenzaMutation : BasicACSDamager { damagetype "Slime" }

actor InfectionCloud : BasicGraphicEffect
{
    Renderstyle "Translucent"
    alpha 0.6
    speed 32
    scale 2.0
    States
    {
    Spawn:
        RZTF AAABBBCCCCZCZCZC 1 A_ScaleVelocity(1.02)
        stop
    }
}

actor RoboenzaDontRip : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_ChangeFlag(DONTRIP, true)
        stop
    }
}

actor RoboenzaDoRip : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_ChangeFlag(DONTRIP, false)
        stop
    }
}

actor FrenzyBlast_P : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_FireCustomMissile("FrenzyBlast")
        stop
    }
}

actor FrenzyBlast : BasicExplosion
{
    Damagetype "FrenzyBlast"
    States
    {
    Spawn:
        TNT1 AA 0 A_Explode(5, 512, 0, 0, 512)
        stop
    }
}

actor RoboenzaPitProtect : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("core_monsterblockplayer", 0, true)
        TNT1 A 0 A_SpawnItemEx("RoboenzaPitProtectWatcher")
        stop
    }
}

actor RoboenzaPitProtectWatcher : BasicWatcher
{
    reactiontime 70
    States
    {
    Spawn:
        TNT1 A 1 A_Countdown
        loop
    Death:
        TNT1 A 0 A_GiveToTarget("RoboenzaPitProtect_Off", 1)
        stop
    }
}

actor RoboenzaPitProtect_Off : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("core_monsterblockplayer", 0, false)
        stop
    }
}
