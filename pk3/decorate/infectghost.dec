const int RBZN_GHOST_BEHIND  = 48;
const int RBZN_GHOST_BASEZ   = 20;
const int RBZN_GHOST_OFFS    = 8;
const int RBZN_GHOST_INCR    = 4;

actor InfectedWalking : Powerup { Powerup.duration 20 }

actor RoboenzaGhostStart_P : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0
        TNT1 A 0 A_PlaySoundEx("rroboenza/player/alphainfect", "voice")
        TNT1 A 0 A_SpawnItemEx("RoboenzaGhostWarper", -RBZN_GHOST_BEHIND, 0, RBZN_GHOST_BASEZ, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        TNT1 A 0
        stop
    }
}

actor RoboenzaGhostWarper : BasicShieldWarper
{
    Renderstyle "Translucent"
    Alpha 0.4
    var int user_floatpos;
	States
	{
	Spawn:
		TNT1 A 0
        TNT1 A 1
		TNT1 A 0 A_GiveInventory("MM8BDMProjSpawnFuncClient",1)
        TNT1 A 0 ACS_NamedExecuteWithResult("RROBOENZ_GHOSTWARPER")
		TNT1 A 0 A_Jump(256, "Shield")
		Goto Shield
    Shield:
	Stand1:
		RBES A 1 A_Warp(AAPTR_TARGET, -RBZN_GHOST_BEHIND, 0, RBZN_GHOST_BASEZ+(sin(user_floatpos)*RBZN_GHOST_OFFS), 0, WARPF_COPYINTERPOLATION|WARPF_NOCHECKPOSITION)
        RBES A 0 A_SetUserVar(user_floatpos, user_floatpos + RBZN_GHOST_INCR)
        RBES A 0 A_JumpIfInTargetInventory("InfectedWalking", 1, "Fly2")
        goto Stand2
    Stand2:
        RBES C 1 A_Warp(AAPTR_TARGET, -RBZN_GHOST_BEHIND, 0, RBZN_GHOST_BASEZ+(sin(user_floatpos)*RBZN_GHOST_OFFS), 0, WARPF_COPYINTERPOLATION|WARPF_NOCHECKPOSITION)
        RBES C 0 A_SetUserVar(user_floatpos, user_floatpos + RBZN_GHOST_INCR)
        RBES C 0 A_JumpIfInTargetInventory("InfectedWalking", 1, "Fly1")
		goto Stand1
    Fly1:
        RBES B 1 A_Warp(AAPTR_TARGET, -RBZN_GHOST_BEHIND, 0, RBZN_GHOST_BASEZ+(sin(user_floatpos)*RBZN_GHOST_OFFS), 0, WARPF_COPYINTERPOLATION|WARPF_NOCHECKPOSITION)
        RBES B 0 A_SetUserVar(user_floatpos, user_floatpos + RBZN_GHOST_INCR * 2)
        RBES B 0 A_JumpIfInTargetInventory("InfectedWalking", 1, "Fly2")
        goto Stand2
    Fly2:
        RBES D 1 A_Warp(AAPTR_TARGET, -RBZN_GHOST_BEHIND, 0, RBZN_GHOST_BASEZ+(sin(user_floatpos)*RBZN_GHOST_OFFS), 0, WARPF_COPYINTERPOLATION|WARPF_NOCHECKPOSITION)
        RBES D 0 A_SetUserVar(user_floatpos, user_floatpos + RBZN_GHOST_INCR * 2)
        RBES D 0 A_JumpIfInTargetInventory("InfectedWalking", 1, "Fly1")
        goto Stand1
    Fade:
        "----" "#" 1 A_FadeOut(0.1)
        wait
	}
}


const int GORE_HORIZ_RANGE = 32;
const int GORE_VERTI_RANGE = 16;

actor RoboenzaGoreWatcherStart_P : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_SpawnItemEx("RoboenzaGoreWatcher",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
        stop
    }
}

actor RoboenzaGoreWatcher : BasicWatcher
{
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_JumpIfInTargetInventory("RoboenzaInfected", 1, "SpawnCheck")
        TNT1 A 0 A_JumpIf(true, "End")
        TNT1 A 1 
        wait
    SpawnCheck:
        TNT1 A 1
        TNT1 A 0 A_JumpIfInTargetInventory("RoboenzaTimer", 21, "Spawn")
        goto SpawnChunks
    SpawnChunks:
        TNT1 A 0 A_Warp(AAPTR_TARGET, frandom(-GORE_HORIZ_RANGE, GORE_HORIZ_RANGE), frandom(-GORE_HORIZ_RANGE, GORE_HORIZ_RANGE), 32+frandom(-GORE_VERTI_RANGE, GORE_VERTI_RANGE), 0, WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("RoboenzaChunk", 0, 0, 0, 0, 0, -frandom(0.5, 2), 0, SXF_TRANSFERTRANSLATION)
        TNT1 A 1 A_SetTics(random(1, 5))
        TNT1 A 0 A_Warp(AAPTR_TARGET, frandom(-GORE_HORIZ_RANGE, GORE_HORIZ_RANGE), frandom(-GORE_HORIZ_RANGE, GORE_HORIZ_RANGE), 28+frandom(-GORE_VERTI_RANGE, GORE_VERTI_RANGE), 0, WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("RoboenzaBloodDrip", frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), 0, 0, -frandom(0.5, 2), 0, SXF_TRANSFERTRANSLATION, 128)
        TNT1 A 5 A_SetTics(random(20, 70))
        goto Spawn
    End:
        TNT1 A 0
        stop
    }
}

actor RoboenzaGore : BasicGraphicEffect
{
    scale 1.5
    speed 100
}

actor RoboenzaChunk : RoboenzaGore
{
    renderstyle "translucent"
    States
    {
    Spawn:
        TNT1 A 0 
        TNT1 A 0 A_SpawnItemEx("RoboenzaBloodDrip", frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), 0, 0, -frandom(0.5, 2), 0, SXF_TRANSFERTRANSLATION, 128)
        TNT1 A 0 A_Jump(128, "Sparks")
        goto Chunk
    Sparks:
        TNT1 AAAAA 0 A_SpawnItemEx("RoboenzaSparks", 0, 0, 0, frandom(-2, 2), frandom(-2, 2), -frandom(0.5, 2), frandom(0, 360), SXF_TRANSFERTRANSLATION)
        TNT1 A 0 A_Jump(128, "Chunk")
        stop
    Chunk:
        MGIB BCDEF 0 A_Jump(255/5, "ChunkLoop")
        MGIB G 0
        goto ChunkLoop
    ChunkLoop:
        "----" "#" 0 A_ChangeVelocity(0, 0, -0.8)
        "----" "#" 1 A_JumpIf(z-floorz <= 1, "ChunkHalt")
        loop
    ChunkHalt:
        "----" "#" 0 A_Stop
        "----" "#" 0 A_Warp(AAPTR_DEFAULT, 0, 0, -(z-floorz), WARPF_NOCHECKPOSITION)
        "----" "#" 10 A_SetTics(random(8, 12))
        "----" "#" 1 A_SetTranslucent(0.0)
        "----" "#" 1 A_SetTranslucent(1.0)
        "----" "#" 1 A_SetTranslucent(0.0)
        "----" "#" 1 A_SetTranslucent(1.0)
        "----" "#" 1 A_SetTranslucent(0.0)
        "----" "#" 1 A_SetTranslucent(1.0)
        "----" "#" 1 A_SetTranslucent(0.0)
        "----" "#" 1 A_SetTranslucent(1.0)
        "----" "#" 1 A_SetTranslucent(0.0)
        "----" "#" 1 A_SetTranslucent(1.0)
        "----" "#" 1 A_SetTranslucent(0.0)
        "----" "#" 1 A_SetTranslucent(1.0)
        stop
    }
}

actor RoboenzaBloodDrip : RoboenzaGore
{
    States
    {
    Spawn:
        RZBL AA 1 A_ChangeVelocity(0, 0, -0.8)
    SpawnLoop:
        RZBL B 0 A_ChangeVelocity(0, 0, -0.8)
        RZBL B 1 A_JumpIf(z-floorz <= 1, "Puddle")
        loop
    Puddle:
        RZBL C 0 A_Stop
        RZBL C 0 A_Warp(AAPTR_DEFAULT, 0, 0, -(z-floorz), WARPF_NOCHECKPOSITION)
        RZBL C 0 A_SpawnItemEx("RoboenzaBloodDropSmall", 0, 0, 0, 3, 0, 3, random(0, 360), SXF_TRANSFERTRANSLATION)
        RZBL C 12 A_SetTics(random(24, 48))
        RZBL E 6
        stop
    }
}

actor RoboenzaBloodDropSmall : RoboenzaGore
{
    States
    {
    Spawn:
        RZBL DDDDDDDD 1 A_ChangeVelocity(0, 0, -0.8)
        stop
    }
}

actor RoboenzaSparks : RoboenzaGore
{
    reactiontime 20
    States
    {
    Spawn:
        RZBL FG 1 A_Countdown
        loop
    Death:
        TNT1 A 0
        stop
    }
}
