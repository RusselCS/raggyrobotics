actor InfectedDrops : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0
        TNT1 A 0 A_JumpIf(!CallACS("core_checkpit"), "Drops")
        stop
    Drops:
        TNT1 A 1 A_SpawnItemEx("RoboenzaBigWeaponEnergy",0,0,28,frandom(0,10),0,0,random(0,360))
        TNT1 AA 1 A_SpawnItemEx("RoboenzaWeaponEnergy",0,0,28,frandom(0,10),0,0,random(0,360))
        stop
    }
}

actor RoboenzaWeaponEnergy : WeaponEnergy
{
    reactiontime 30
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("RoboenzaWEWatcher")
        goto Super::Spawn
    Pickup:
        TNT1 A 0 A_JumpIfInventory("RoboenzaInfected", 1, "Infected")
        goto Super::Pickup
    Infected:
        TNT1 A 0
        fail
    Death:
        TNT1 A 0 A_GiveInventory("RoboenzaInfected", 1)
        TNT1 A 1
        stop
    }
}

actor RoboenzaBigWeaponEnergy : BigWeaponEnergy
{
    reactiontime 30
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("RoboenzaWEWatcher")
        goto Super::Spawn
    Pickup:
        TNT1 A 0 A_JumpIfInventory("RoboenzaInfected", 1, "Infected")
        goto Super::Pickup
    Infected:
        TNT1 A 0
        fail
    Death:
        TNT1 A 0 A_GiveInventory("RoboenzaInfected", 1)
        TNT1 A 1
        stop
    }
}

actor RoboenzaWEWatcher : BasicWatcher
{
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_SetTics(random(0, 35))
    SpawnLoop:
        TNT1 A 35
        TNT1 A 0 A_GiveToTarget("RoboenzaWECountdown_P", 1)
        TNT1 A 0 A_JumpIfInventory("RoboenzaInfected", 1, "End")
        loop
    End:
        TNT1 A 0
        stop
    }
}

actor RoboenzaWECountdown_P : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_Countdown
        stop
    }
}

actor RoboenzaCure : CustomInventory
{
    inventory.pickupmessage "Roboenza Cure!"
    scale 2.5
    reactiontime 1050
    States
    {
    Spawn:
        RZCR A 0
        RZCR A 0 A_ChangeVelocity(frandom(0, 8), frandom(0, 8), frandom(0, 10), CVF_RELATIVE|CVF_REPLACE)
        RZCR A 0 A_PlaySoundEx("rroboenza/powerups/curespawn", "voice")
    SpawnLoop:
        RZCR AAABBBCCCDDDEEEFFFGGGHHH 1 A_Countdown
        loop
    Death:
        RZCR A 0
        stop

    Pickup:
        RZCR A 0 A_JumpIfInventory("RoboenzaInfected", 1, "CureMe")
        fail
    CureMe:
        RZCR A 0 ACS_NamedExecuteWithResult("RROBOENZ_CURE")
        stop
    }
}

actor RoboenzaCureEffect : BasicWatcher
{
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_PlaySoundEx("rroboenza/powerups/cureact", "voice")
        TNT1 A 0 A_SpawnItemEx("RoboenzaCureFog")
        stop
    }
}

actor RoboenzaCureFog : BasicGraphicEffect
{
    States
    {
    Spawn:
        RZTF DEFGH 2
        stop
    }
}
