#library "RROBOMAP"

#include "zcommon.acs"
#include "CROCFUNC.acs"

int hasInfected = false;

#define DEATHS_MAX 4

#define SCRIPT_MAP 0
#define SCRIPT_NAME 1

str deathScripts[DEATHS_MAX][2] = {
    { "MM10DW1", "death_music"}, //  MM10DW1 - Wily Station Archives          -- MM8BDM-v6b.pk3
    {   "BON04", "death_music"}, //    BON04 - Welcome Home                   -- 8bdm_jampack-v5d.pk3
    {"MME10DW1", "death_music"}, // MME10DW1 - Wily Station Archives: Encore! -- mm8bdm-encore-beta6h2-mapsonly.pk3
    {  "MMEPRO", "death_music"}, //   MMEPRO - Proto Man                      -- mm8bdm-encore-beta6h2-mapsonly.pk3
};

// runs the death scripts if on a map that needs them
//
script "rroboenz_map_infect" (void)
{
    if(hasInfected)
        terminate;
    
    hasInfected = true;
    str map = StrParam(n:PRINTNAME_LEVEL);

    for(int i = 0; i < DEATHS_MAX; i++) {
        if(strICmp(deathScripts[i][SCRIPT_MAP], map) == 0) {
            CallACS0(deathScripts[i][SCRIPT_NAME]);
            terminate;
        }
    }
    Log(s:"Map not found.");
}

// This is just a clone of vanilla 8bdm's teleport functionality to work in older maps.
// This script does another, oddly-specific thing for one, oddly-specific map.
//
#define TPSAFE 2984

script TPSAFE (int spot, int sect, int source)
{
	if(CheckInventory("SafeTPFlag"))
		terminate;
    
    if(!CustomLevelCheck())
        terminate;

	GiveInventory("SafeTPFlag", 1);
	
	Teleport(spot, sect, source);
}

int customLevel = -2;

#define TP_MAX 1

str teleportScripts[TP_MAX][2] = {
    {"EMIMAP09", "rroboenz_skeldcheck"} // EMIMAP09 - The Skeld --- emilianos-dumb-pack-v2.2a.pk3
};

function int customLevelCheck(void)
{
    if(customLevel == -2) {
        customLevel = -1;

        str map = StrParam(n:PRINTNAME_LEVEL);
        for(int i = 0; i < TP_MAX && customLevel < 0; i++) {
            if(strICmp(teleportScripts[i][SCRIPT_MAP], map) == 0) {
                customLevel = i;
            }
        }
    }

    if(customLevel == -1)
        return true;
    
    return CallACS0(teleportScripts[customLevel][SCRIPT_NAME]);
}

// On The Skeld, only infected players can use the vents.
script "rroboenz_skeldcheck" (void) { SetResultValue(CheckInventory("RoboenzaInfected") > 0); }
