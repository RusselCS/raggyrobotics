#library "RROBOMSG"

#include "zcommon.acs"

#include "CROCFUNC.acs"
#include "RROBCOMM.acs"

#include "BARLIB.acs"
#include "ASSTLIB.acs"

// All the clientside scripts go here.

// script "RR_TESTMSG" (void)
// {
//     for(int i = -RANGE; i < -1*RANGE/FRAG_BOT*-FRAC_TOP; i+=FRAG_PAGE) {
//         CallACS2("RR_CLEARMSGRANGE", i, i+FRAG_PAGE);
//     }
//     delay(1);
//     //restart;
// }

// script "RR_CLEARMSGRANGE" (int low, int high)
// {
//     for(int i = low; i < high; i++) {
//         HudMessageBold(s:""; HUDMSG_PLAIN, i, CR_UNTRANSLATED, 0, 0, 0);
//     }
// }

Script "RROBOENZ_COUNTER" (void) //OPEN CLIENTSIDE
{
	PrintBold(s:"");
	Delay(5);

    while(!CallACS0("RROBOENZ_PICKTIME"))
        delay(1);

	while(canRoboenza())
	{
		SetFont("SMALLFONT");
		HudMessageBold(s:"Infected: ", i:playerCountTeam(TEAM_INFECTED); HUDMSG_PLAIN, MSGID_INFECTSCOUNT, GetTeamProperty(TEAM_INFECTED, TPROP_TextColor), 0.04, 0.93, 1.0,0);
		HudMessageBold(s:"Survivors: ", i:playerCountTeam(TEAM_SURVIVOR); HUDMSG_PLAIN, MSGID_SURVIVECOUNT, GetTeamProperty(TEAM_SURVIVOR, TPROP_TextColor), 0.04, 0.95, 1.0,0);
		Delay(1);
	}
}

script "RROBOENZ_ALERT" (int who, int type, int del) CLIENTSIDE
{
    int cln = ConsolePlayerNumber();
    int cam = checkPlayerCameraPln(cln);
    SetFont("BIGFONT");
    delay(del);
    switch(type) {
        default:
        case MSG_INFECTED:
            HudMessageBold(n:who+1, l:"RBNZ_AL_INFECT";  HUDMSG_FADEOUT, MSGID_ALERT, CR_YELLOW,0.5, 0.90, 1.8,0.2);
            if(cam != who && GetUserCVar(cln, "rroboenz_infectsound")) AmbientSound("rroboenza/ui/infect", 128);
            break;
        case MSG_CURED:
            HudMessageBold(n:who+1, l:"RBNZ_AL_CURED";   HUDMSG_FADEOUT, MSGID_ALERT, CR_BLUE,  0.5, 0.90, 1.8,0.2);
            if(cam != who && GetUserCVar(cln, "rroboenz_curedsound")) AmbientSound("rroboenza/ui/cure", 128);
            break;
        case MSG_ENRAGED:
            HudMessageBold(n:who+1, l:"RBNZ_AL_ENRAGE";  HUDMSG_FADEOUT, MSGID_ALERT, CR_RED,   0.5, 0.90, 1.8,0.2);
            if(cam != who && GetUserCVar(cln, "rroboenz_enragesound")) AmbientSound("rroboenza/ui/rage", 128);
            break;
        case MSG_BERSERK:
            HudMessageBold(n:who+1, l:"RBNZ_AL_BERSERK"; HUDMSG_FADEOUT, MSGID_ALERT, CR_RED,   0.5, 0.90, 1.8,0.2);
            if(cam != who && GetUserCVar(cln, "rroboenz_berserksound")) AmbientSound("rroboenza/ui/berserk", 128);
            break;
    }
}

script "RROBOENZ_TOOLTIP" (int hp, int alpha, int who) CLIENTSIDE
{
    SetFont("SMALLFONT");
    if(alpha) {
        HudMessage(l:"RBNZ_TIP_A1"; HUDMSG_FADEOUT, MSGID_ZOMBIETIP1, CR_YELLOW,0.5, 0.14, 3.0,2.0);
        HudMessage(l:"RBNZ_TIP_A2"; HUDMSG_FADEOUT, MSGID_ZOMBIETIP2, CR_RED,1.5, 0.18, 3.0,2.0);
        HudMessage(l:"RBNZ_TIP_A3", i:hp; HUDMSG_FADEOUT, MSGID_ZOMBIETIP3, CR_WHITE,0.5, 0.26, 3.0,2.0);
        Print(l:"RBNZ_TIP_A1", s:"\n", l:"RBNZ_TIP_A2", s:"\n", l:"RBNZ_TIP_A3", i:hp);
        Print(s:"");
    } else {
        if(who > 0) {
            Print(n:who, l:"RBNZ_TIP_1F", s:"\n", l:"RBNZ_TIP_2", s:"\n", l:"RBNZ_TIP_3", i:hp);
            HudMessage(n:who, l:"RBNZ_TIP_1F"; HUDMSG_FADEOUT, MSGID_ZOMBIETIP1, CR_YELLOW,0.5, 0.14, 3.0,2.0);
        } else {
            Print(l:"RBNZ_TIP_1", s:"\n", l:"RBNZ_TIP_2", s:"\n", l:"RBNZ_TIP_3", i:hp);
            HudMessage(l:"RBNZ_TIP_1"; HUDMSG_FADEOUT, MSGID_ZOMBIETIP1, CR_YELLOW,0.5, 0.14, 3.0,2.0);
        }
        HudMessage(l:"RBNZ_TIP_2"; HUDMSG_FADEOUT, MSGID_ZOMBIETIP2, CR_RED,1.5, 0.18, 3.0,2.0);
        HudMessage(l:"RBNZ_TIP_3", i:hp; HUDMSG_FADEOUT, MSGID_ZOMBIETIP3, CR_WHITE,0.5, 0.26, 3.0,2.0);
        Print(s:"");
    }
}

script "RROBOENZ_YOUINFECTED" (int attacker, int victim) CLIENTSIDE
{
    int cln = ConsolePlayerNumber();
    if(attacker != cln)
        terminate;

    str fragMsg = StrParam(s:"YOU INFECTED ", n:victim+1, s:"!");
    
    if(!GetCvar("CL_ShowLargeFragMessages")) {
        Log(s:fragMsg);
    }

    SetFont("BIGFONT");
    HudMessageBold(s:fragMsg; HUDMSG_FADEOUT, MSGID_YOUINFECTED1, CR_DARKGREEN, 1.5, 0.325, 2.5, 0.5);
    SetFont("SMALLFONT");

    int survivorCount = playerCountTeam(TEAM_SURVIVOR);
    if(survivorCount == 1) {
        HudMessageBold(s:"ONE SURVIVOR REMAINS!"; HUDMSG_FADEOUT, MSGID_YOUINFECTED2, CR_YELLOW, 1.5, 0.375, 2.5, 0.5);
    } else {
        HudMessageBold(i:survivorCount, s:" SURVIVORS REMAIN."; HUDMSG_FADEOUT, MSGID_YOUINFECTED2, CR_YELLOW, 1.5, 0.375, 2.5, 0.5);
    }
}

script "RROBOENZ_FYI" (int which) CLIENTSIDE
{
    Log(s:"(Winlimit is automatically changed every round to manage a match's end state.)");
}

script "RROBOENZ_APPLYZOMBIESKIN" (void) CLIENTSIDE
{
    delay(1);
    int cpn = ConsolePlayerNumber();
	if(cpn!=PlayerNumber()){terminate;}

    str skin = GetCvarString("rroboenz_zombieskin");

    if(StrICmp(skin, "") != 0) {
        LogConsoleCommand("rroboenz_tempskin $skin");
        LogConsoleCommand(StrParam(s:"skin \"", s:skin, s:"\""));
    }
}

script "RROBOENZ_REVOKEZOMBIESKIN" (void) CLIENTSIDE
{
    delay(1);
    int cpn = ConsolePlayerNumber();
	if(cpn!=PlayerNumber()){terminate;}

    str zombie = GetCvarString("rroboenz_zombieskin");

    if(StrICmp(zombie, "") == 0) {
        SetCvarString("rroboenz_tempskin", "");
    } else {
        LogConsoleCommand("rroboenz_compskin $skin");
        delay(1);

        str temp = GetCvarString("rroboenz_tempskin");
        str comp = GetCvarString("rroboenz_compskin");

        if(StrICmp(temp, "") != 0) {
            if(StrICmp(zombie, comp) == 0) { // If the player changed their skin in the middle of this, it should just accept the new skin as uninfect.
                LogConsoleCommand(StrParam(s:"skin \"", s:temp, s:"\""));
            }
            SetUserCvarString(cpn, "rroboenz_tempskin", "");
        }
    }
}

script "rroboenz_resetzombieskin" (void) NET CLIENTSIDE
{
    Delay(1);

	int cpn = ConsolePlayerNumber();
	if(cpn!=PlayerNumber()){terminate;}

    SetUserCvarString(cpn, "rroboenz_zombieskin", "");
    LogConsoleCommand("openmenu menu_rroboenz");
}

script "rroboenz_copyskintozombie" (void) NET CLIENTSIDE
{
    Delay(1);

	int cpn = ConsolePlayerNumber();
	if(cpn!=PlayerNumber()){terminate;}
    LogConsoleCommand("rroboenz_zombieskin_stage $skin");
    delay(1);
    SetUserCvarString(cpn, "rroboenz_zombieskin", GetCvarString("rroboenz_zombieskin_stage"));
    LogConsoleCommand("openmenu menu_rroboenz");
}

function void LogConsoleCommand(str cmd) {
    Log(s:"Running console command: ", s:cmd);
    ConsoleCommand(cmd);
}

script "DrawBar_RoboenzaWep" (void) CLIENTSIDE
{
    int cpn = ConsolePlayerNumber();

    SetMugshotColorGlow(CLR_INF_CYAN,CLR_INF_BLUE,CLR_INF_BLACK);

    if(GetUserCvar(cpn,"rroboenz_timerbar") == 1) {
        DrawBarColorGlow(CLR_INF_CYAN,CLR_INF_BLUE,CLR_INF_BLACK);
    }
}

#define BTN_TIME 8

str panicButtonCommands[4] = {
    "usepanicbutton",
    "+reload",
    "+use",
    "+crouch"
};

script "RROBOENZ_UIHANDLER" (int cam) CLIENTSIDE
{
    int cpn = ConsolePlayerNumber();

    if(roboenzaBarCheck()) {
        if(AssistDisplayBar_On()) {
            SetHudSize(320,200,0);
            int pos;
            str img;
            int x;
            int y;
            int timeSec;

            SetActivator(cam);
            int time = CheckActorInventory(cam,"RoboenzaTimer");
            int timeMax = GetAmmoCapacity("RoboenzaTimer");
            SetActivator(0,AAPTR_NULL);

            if(GetUserCvar(cpn,"rroboenz_timerbar") == 2 && CheckActorInventory(cam,"RoboenzaTimer")>0) {
                pos = Get_And_Inc_AssistBarCount();
                int barX = Get_AssistDisplayBar_X(pos);
                int barY = Get_AssistDisplayBar_Y(pos);
                x = barX - 4.0;
                y = barY - 4.0;
                img = "INFECT1";
                if(CheckActorInventory(cam, "RoboenzaTimer") <= 1 && Timer() % 8 <= 3) img = "TNT1A0";
                BasicImageDisplay(img,Get_And_Inc_AssistDisplayID(),x,y);
                DrawBasicBarAndNumber(
                    "ASTBRRBZ","ASTBRRBE",
                    "ASTVRRBZ","ASTVRRBE",
                    barX,barY,
                    Get_And_Inc_AssistDisplayID(),Get_And_Inc_AssistDisplayID(),Get_And_Inc_AssistDisplayID(),
                    ((time * 8.0 / timeMax) + 0.5) >> 16,
                    CheckActorInventory(cam,"AssistNumberFlag")>0,
                    CheckActorInventory(cam,"RoboenzaTimer")
                );
            }

            if(GetUserCvar(cpn, "rroboenz_alphabuffast") && CheckActorInventory(cam, "RoboenzaAlphaInfected")) {
                pos = Get_And_Inc_AssistBarCount();
                BasicImageDisplay("RBNZPZRI",Get_And_Inc_AssistDisplayID(),
                    Get_AssistDisplayBar_X(pos),
                    Get_AssistDisplayBar_Y(pos)
                );
            }

            if(GetUserCvar(cpn, "rroboenz_ragebuffast") && CheckActorInventory(cam, "FrenzyBuffStack") == 1) {
                pos = Get_And_Inc_AssistBarCount();
                BasicImageDisplay("RBNZRAGI",Get_And_Inc_AssistDisplayID(),
                    Get_AssistDisplayBar_X(pos),
                    Get_AssistDisplayBar_Y(pos)
                );
            }

            if(GetUserCvar(cpn, "rroboenz_spreadbuffast") && CheckActorInventory(cam, "FrenzyBuffStack") == 2) {
                pos = Get_And_Inc_AssistBarCount();
                BasicImageDisplay("RBNZBRZI",Get_And_Inc_AssistDisplayID(),
                    Get_AssistDisplayBar_X(pos),
                    Get_AssistDisplayBar_Y(pos)
                );
            }

            if(GetUserCvar(cpn, "rroboenz_panicicon") && hasPanicItem(cam)) {
                pos = Get_And_Inc_AssistBarCount();
                int bindIdx = GetUserCvar(cpn, "rroboenz_panicbutton")-1;
                if(bindIdx >= 0) {
                    str bindStr;
                    x = Get_AssistDisplayBar_X(pos);
                    y = Get_AssistDisplayBar_Y(pos);
                    int tX = x-2.0;
                    int tY = y+10.0;

                    if(timer() % (BTN_TIME * 2) < BTN_TIME) {
                        bindStr = StrParam(s:"\c[BTNAL1]", s:getInput(panicButtonCommands[GetUserCvar(cpn, "rroboenz_panicbutton")-1], 0));
                    } else {
                        bindStr = StrParam(s:"\c[BTNAL2]", s:getInput(panicButtonCommands[GetUserCvar(cpn, "rroboenz_panicbutton")-1], 0));
                    }
                    
                    if(GetChar(bindStr, 0) != '?') {
                        BasicTextDisplay("SMALLFONT", bindStr, Get_And_Inc_AssistDisplayID(),tX,tY);
                    }
                }

                if(CheckActorInventory(cam, "ExitUnitItem") > 0) {
                    BasicImageDisplay("RZEXTA",Get_And_Inc_AssistDisplayID(),x,y);
                } else if(CheckActorInventory(cam, "FrenzyBuffItem") > 0) {
                    BasicImageDisplay("RZFRZA",Get_And_Inc_AssistDisplayID(),x,y);
                }
            }
        }
    }

    // turns out this needs to be done via the actual inventory manager script...
    // if(GetUserCvar(cpn,"rroboenz_timerbar") == 3) {
    //     int count = CheckActorInventory(cam, "RoboenzaTimer");
    //     if(count > 0) {
    //         SetActorInventory(cam, "RoboenzaTimerInvBar", count);
    //     } else {
    //         SetActorInventory(cam, "RoboenzaTimerInvBar", 0);
    //     }
    // } else {
    //     SetActorInventory(cam, "RoboenzaTimerInvBar", 0);
    // }
}

function bool roboenzaBarCheck(void) {
    int cpn = ConsolePlayerNumber();

    int ret = GetUserCvar(cpn,"rroboenz_timerbar") == 2 ||
              GetUserCvar(cpn, "rroboenz_alphabuffast") ||
              GetUserCvar(cpn, "rroboenz_ragebuffast") ||
              GetUserCvar(cpn, "rroboenz_spreadbuffast") ||
              GetUserCvar(cpn, "rroboenz_panicicon");
    
    return ret;
}

function bool hasPanicItem(int tid) {
    return CheckActorInventory(tid, "ExitUnitItem") > 0 || CheckActorInventory(tid, "FrenzyBuffItem") > 0;
}

script "rroboenz_changes_v1f" (void) NET CLIENTSIDE
{
    Log(s:"====================== v1fh ======================");
    Log(s:"* Fixed SCORINFO not allowing the game to run with latest Zandronum alpha.");
    Log(s:"* Fixed an issue where infected skins would not apply when the skin name had spaces.");
    Log(s:"* Tweaked default CVars to work around some existing bugs with alpha queue system.");
    Log(s:"* Added a message to explain to players why the winlimit keeps changing.");
    Log(s:"* Made some tweaks to the backend for compatibility with Binary's modevote.");
    Log(s:"* Made some tweaks to the backend to begin compatibility with class mods.");
    Log(s:"=================================================");
}

script "rroboenz_changes_v1e" (void) NET CLIENTSIDE
{
    Log(s:"====================== v1e ======================");
    Log(s:"* Added \"Strain\": How many infections did you and your underlings score?");
    Log(s:"* Updated infected health calculation to be less feast or famine.");
    Log(s:"* Added a CVar \"rroboenz_sv_infectlifefactor\" to control infected health per server.");
    Log(s:"* Added a queue system to choose Patient Zero instead of pure RNG.");
    Log(s:"* Fixed a bug where a script would force everyone to select \"RoboenzaWep\" every tic.");
    Log(s:"* Infected players now become unsolid temporarily when infected if \"sv_unblockplayers\" is active.");
    Log(s:"* In the rare event a player dies from an infection attack, the attacker now:");
    Log(s:"*** Heals 20% of their max health (instead of +20 flat)");
    Log(s:"*** Gains 20% lifetime (instead of +20 seconds flat)");
    Log(s:"*** Gains Patient Zero buff");
    Log(s:"* Added cvars to enable/disable potentially gamebreaking features.");
    Log(s:"* Added something to fix broken map teleporters.");
    Log(s:"* Scoring Infections now triggers death events on certain maps.");
    Log(s:"* UIX Weapon Bar is now hidden while infected.");
    Log(s:"=================================================");
}

script "rroboenz_changes_v1dh1" (void) NET CLIENTSIDE
{
    Log(s:"====================== v1dh ======================");
    Log(s:"* Fixed an issue introduced by the latest Zandronum beta.");
    Log(s:"* Server CVar added to control zombie lifetime.");
    Log(s:"=================================================");
}

script "rroboenz_changes_v1d" (void) NET CLIENTSIDE
{
    Log(s:"====================== v1d ======================");
    Log(s:"* Fixed an issue where being cured would keep RoboenzaWep.");
    Log(s:"* Last survivor is given +2 frenzy powerups if 66% of all players are infected.");
    Log(s:"* Game no longer picks any previous alphas as new alphas, unless contingency happens.");
    Log(s:"* Clients are told to select RoboenzaWep when infected. Should fix any remaining weapon desyncs.");
    Log(s:"* Infected players knockback buffer reduced to 2 ticks (from 4)");
    Log(s:"* Infection claw hitbox reworked to use explosions instead of projectiles. Removed player hitbox infector.");
    Log(s:"* Infected players no longer have a special landing sound (it was too inconsistent).");
    Log(s:"=================================================");
}

script "rroboenz_changes_v1c" (void) NET CLIENTSIDE
{
    Log(s:"====================== v1c ======================");
    Log(s:"* Infected super jump no longer consumes time.");
    Log(s:"* Fixed a bug where the game manager would die.");
    Log(s:"* Max cure percentage reduced to 20%.");
    Log(s:"* Infected/Patient Zero knockback now has a 4-tick buffer.");
    Log(s:"* Players w/ Handicapped HP below 20 get infected.");
    Log(s:"* Roboenza Frenzy options links to correct Player Setup.");
    Log(s:"* More patients zero are picked w/ high player counts.");
    Log(s:"* Patient Zero instant spectate/suicide no longer skips map.");
    Log(s:"* Survivors now properly draw Dash-type items (Item-2).");
    Log(s:"* Infected now properly heal when above 100 HP.");
    Log(s:"* Infecteds now visibly fall apart when near timeout.");
    Log(s:"* Added a message to display if you infect someone.");
    Log(s:"* Infection claw now infects people inside the user.");
    Log(s:"* Infection claw is slightly bigger + ignores shields.");
    Log(s:"=================================================");
}

script "rroboenz_changes_v1b" (void) NET CLIENTSIDE
{
    Log(s:"====================== v1b ======================");
    Log(s:"* Cured and final survivors now blast away close Infected.");
    Log(s:"* Cure now appears on automap.");
    Log(s:"* Options menu no longer jumps to Legacy settings.");
    Log(s:"* Assist Bar now shows this mod's buffs.");
    Log(s:"* Fixed map voting playing poorly with this mod.");
    Log(s:"* Infected can no longer pick up ammo.");
    Log(s:"* Scoreboard infections now reset on map change.");
    Log(s:"* Shield effects stick around after infection");
    Log(s:"* Fixed infected/spectators damaging survivors.");
    Log(s:"* Infected landing should be more reliable.");
    Log(s:"* Infected health regen should be more reliable.");
    Log(s:"* Players should no longer get kicked for user info flood.");
    Log(s:"* Intense/Victory music should now play at correct times.");
    Log(s:"* Infected should no longer be able to use copyweps.");
    Log(s:"==============================================");
}
